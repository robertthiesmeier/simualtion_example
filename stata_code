/* Example of a simple prospective cohort study 

 X = exposure
 Y = outcome 
 
 P(X) = .2
 P(Y=1|X=0) = .1 
 P(Y=1|X=1) = .3
 OR = [.3/(1-.3)]/[.1/(1-.1)]
 
*/

// create a program to simualte the study

capture program drop sim_study
program define sim_study, rclass
	drop _all 
	local n = 1000
	local y_1 = .3
	local y_0 = .1
	local x_p = .2
	
	qui set obs `n'
	qui gen x = rbinomial(1, `x_p')
	qui gen y = .
	qui replace y = rbinomial(1, `y_1') if x == 1
	qui replace y = rbinomial(1, `y_0') if x == 0

	logit y x
	return scalar est_y = _b[x]
end

// simulate the study
sim_study 

// simulate the study 1,000 times
simulate est_y = r(est_y), reps(1000) seed(130122): sim_study 

// summary statistics
summarize est_y 

gen or = exp(est_y)

// figure
tw (kdensity or, recast(area) color(blue%30)), ///
	ytitle("Simulated Distribution") /// 
	xtitle("Simulated Odds Ratio")
